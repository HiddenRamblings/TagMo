apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.hiddenramblings.tagmo"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 11
        versionName "2.7.6"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()]
            }
        }

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "TagMo-" + commit
    }

    buildTypes {
        release {
            getIsDefault().set(true)
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            initWith release
            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
                signingConfigs {
                    debug {
                        keyAlias keystoreProps['keyAlias']
                        keyPassword keystoreProps['keyPassword']
                        storeFile file(keystoreProps['storeFile'])
                        storePassword keystoreProps['storePassword']
                    }
                }
                buildTypes.debug.signingConfig signingConfigs.debug
            } catch (FileNotFoundException e) {
                // Github actions can go $#%& right off with this
            }
        }
        publish {
            initWith release
            applicationIdSuffix ".eightbit"
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    ndkVersion "21.4.7075529"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs[0].outputFileName = project.archivesBaseName + ".apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.robertlevonyan.view:MaterialChipView:2.2.7'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    annotationProcessor "org.androidannotations:androidannotations:4.6.0"
    implementation "org.androidannotations:androidannotations-api:4.6.0"
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'io.github.vicmikhailau:MaskedEditText:4.0.7'
    // Required for Lollipop Amiibo API download compatibility
    implementation 'com.google.android.gms:play-services-safetynet:17.0.1'
}
