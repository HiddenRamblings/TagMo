plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp' version '1.9.10-1.0.13'
}

android {
    defaultConfig {
        applicationId 'com.hiddenramblings.tagmo'
        compileSdk 34
        minSdk 19
        targetSdk 34
        versionName "4.1.3"
        versionCode = versionName.replace(".","").toInteger() * 10

        vectorDrawables {
            useSupportLibrary = true
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "${rootProject.name}-${commit}"
    }

    namespace defaultConfig.applicationId

    flavorDimensions += "source"
    productFlavors {
        github {
            dimension "source"
            manifestPlaceholders.storagePermission = "android.permission.MANAGE_EXTERNAL_STORAGE"
            manifestPlaceholders.installPermission = "android.permission.REQUEST_INSTALL_PACKAGES"
            buildConfigField "boolean", "GOOGLE_PLAY", "false"
        }
        google {
            dimension "source"
            isDefault true
            manifestPlaceholders.storagePermission = "com.hiddenramblings.tagmo.BROWSER"
            manifestPlaceholders.installPermission = "com.hiddenramblings.tagmo.UPDATING"
            buildConfigField "boolean", "GOOGLE_PLAY", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".eightbit"
            manifestPlaceholders.wearUsesFeature = "android.hardware.usb.host"
            buildConfigField "boolean", "WEAR_OS", "false"
        }
        debug {
            initWith buildTypes.release
            debuggable true
        }
        wearos {
            initWith buildTypes.release
            versionNameSuffix "-wear"
            manifestPlaceholders.wearUsesFeature = "android.hardware.type.watch"
            buildConfigField "boolean", "WEAR_OS", "true"
        }
    }

    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
        buildTypes.wearos.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    ndkVersion '21.4.7075529'
    externalNativeBuild {
        cmake {
            path "libs/CMakeLists.txt"
        }
    }

    def javaVersion = JavaVersion.VERSION_17
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    lint {
        abortOnError false
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name == buildTypes.wearos.name) {
                output.versionCodeOverride = defaultConfig.versionCode + 1
            }
            output.outputFileName = project.archivesBaseName + "-${output.baseName}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-ktx:1.8.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.browser:browser:1.7.0"
    implementation 'androidx.camera:camera-core:1.3.0'
    implementation 'androidx.camera:camera-camera2:1.3.0'
    implementation 'androidx.camera:camera-lifecycle:1.3.0'
    implementation 'androidx.camera:camera-view:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.webkit:webkit:1.8.0'
    wearosImplementation "androidx.wear:wear:1.3.0"

    implementation 'com.android.billingclient:billing:6.1.0'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    ksp 'com.github.bumptech.glide:ksp:4.16.0'

    implementation 'com.github.8bitdream:AndroidFastScroll:4cb96477d5'

    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // Required for Lollipop Amiibo API download compatibility
    implementation 'com.google.android.gms:play-services-basement:18.2.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.android.play:core:1.10.3'
    // implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.1.0'
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'
    implementation 'com.google.zxing:core:3.5.0'

    // Current (3.0.5) requires kotlin and incompatible with Wear
    implementation 'com.robertlevonyan.view:MaterialChipView:3.0.4'

    implementation 'io.github.vicmikhailau:MaskedEditText:4.0.7'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
}