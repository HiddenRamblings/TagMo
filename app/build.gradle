plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId 'com.hiddenramblings.tagmo'
        minSdkVersion 16
        targetSdkVersion 33
        versionName "3.5.5"
        versionCode = versionName.replace(".","").toInteger() * 10

        vectorDrawables {
            useSupportLibrary = true
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "${rootProject.name}-${commit}"
    }

    namespace defaultConfig.applicationId

    flavorDimensions "source"
    productFlavors {
        github {
            dimension "source"
            manifestPlaceholders.storagePermission = "android.permission.MANAGE_EXTERNAL_STORAGE"
            manifestPlaceholders.installPermission = "android.permission.REQUEST_INSTALL_PACKAGES"
            manifestPlaceholders.hasFragileUserData = "true"
            buildConfigField "boolean", "GOOGLE_PLAY", "false"
        }
        google {
            dimension "source"
            isDefault true
            manifestPlaceholders.storagePermission = "com.hiddenramblings.tagmo.BROWSER"
            manifestPlaceholders.installPermission = "com.hiddenramblings.tagmo.UPDATING"
            manifestPlaceholders.hasFragileUserData = "false"
            buildConfigField "boolean", "GOOGLE_PLAY", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".eightbit"
            manifestPlaceholders.wearUsesFeature = "android.hardware.screen.portrait"
            buildConfigField "boolean", "WEAR_OS", "false"
        }
        debug {
            initWith buildTypes.release
            debuggable true
        }
        wearos {
            initWith buildTypes.release
            versionNameSuffix "-wear"
            manifestPlaceholders.wearUsesFeature = "android.hardware.type.watch"
            buildConfigField "boolean", "WEAR_OS", "true"
        }
    }

    sourceSets {
        release {
            res.srcDirs('src/shared/res')
        }
        debug {
            res.srcDirs('src/shared/res')
        }
    }

    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
        buildTypes.wearos.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    ndkVersion '21.4.7075529'
    externalNativeBuild {
        cmake {
            path "libs/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name == buildTypes.wearos.name) {
                output.versionCodeOverride = defaultConfig.versionCode + 1
            }
            output.outputFileName = project.archivesBaseName + "-${output.baseName}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.browser:browser:1.4.0"
    implementation 'androidx.camera:camera-core:1.3.0-alpha02'
    implementation 'androidx.camera:camera-camera2:1.3.0-alpha02'
    implementation 'androidx.camera:camera-lifecycle:1.3.0-alpha02'
    implementation 'androidx.camera:camera-view:1.3.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.webkit:webkit:1.5.0'
    wearosImplementation "androidx.wear:wear:1.2.0"

    implementation 'com.android.billingclient:billing:5.1.0'

    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    kapt 'com.github.bumptech.glide:compiler:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'

    implementation 'com.github.8bitDream:android-issue-reporter:15cdc1024d'

    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // Required for Lollipop Amiibo API download compatibility
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
    // implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.1.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.mlkit:barcode-scanning:17.0.3'
    implementation 'com.google.zxing:core:3.5.0'

    // Current (3.0.5) requires kotlin and incompatible with Wear
    implementation 'com.robertlevonyan.view:MaterialChipView:3.0.4'

    implementation 'io.github.ShawnLin013:number-picker:2.4.13'
    implementation 'io.github.vicmikhailau:MaskedEditText:4.0.7'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}