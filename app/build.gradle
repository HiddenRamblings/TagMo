apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.hiddenramblings.tagmo"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 12
        versionName "2.7.9"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()]
            }
        }

        vectorDrawables {
            useSupportLibrary = true
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "TagMo-" + commit
    }

    buildTypes {
        release {
            getIsDefault().set(true)
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            initWith release
            debuggable true
            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
                signingConfigs {
                    debug {
                        keyAlias keystoreProps['keyAlias']
                        keyPassword keystoreProps['keyPassword']
                        storeFile file(keystoreProps['storeFile'])
                        storePassword keystoreProps['storePassword']
                    }
                }
                buildTypes.debug.signingConfig signingConfigs.debug
            } catch (FileNotFoundException ignored) { }
        }
        publish {
            initWith release
            applicationIdSuffix ".eightbit"
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    ndkVersion "21.4.7075529"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs[0].outputFileName = project.archivesBaseName + ".apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.google.android.material:material:1.5.0'
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.webkit:webkit:1.4.0'
    api 'androidx.preference:preference:1.2.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    api "androidx.browser:browser:1.4.0"
    api "org.androidannotations:androidannotations-api:4.6.0"
    annotationProcessor "org.androidannotations:androidannotations:4.6.0"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.robertlevonyan.view:MaterialChipView:2.2.7'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'io.github.vicmikhailau:MaskedEditText:4.0.7'
    implementation 'com.github.SamSprung:android-issue-reporter:ce3838f'
    // Required for Lollipop Amiibo API download compatibility
    api 'com.google.android.gms:play-services-safetynet:18.0.1'
}
