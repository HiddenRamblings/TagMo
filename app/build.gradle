import org.gradle.internal.os.OperatingSystem
apply plugin: 'com.android.application'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.hiddenramblings.tagmo"
        minSdkVersion 16
        targetSdkVersion 32
        versionCode 307
        versionName "3.0.7"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()]
            }
        }

        vectorDrawables {
            useSupportLibrary = true
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "TagMo-" + commit
    }

    def branch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    if (OperatingSystem.current().isMacOsX()) branch = branch.replaceAll("heads/", "")

    buildTypes {
        release {
            getIsDefault().set(true)
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (branch == "master" || branch == "conversion") applicationIdSuffix ".eightbit"
        }
        debug {
            initWith release
            getIsDefault().set(false)
            debuggable true
        }
        publish {
            initWith release
            getIsDefault().set(false)
            if (branch != "master" && branch != "conversion") applicationIdSuffix ".eightbit"
        }
    }

    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
        buildTypes.publish.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    ndkVersion '21.4.7075529'
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs[0].outputFileName = project.archivesBaseName + ".apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.fragment:fragment:1.5.2'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.browser:browser:1.4.0"
    implementation 'com.android.billingclient:billing:5.0.0'
    implementation 'com.google.android.play:core:1.10.3'
    implementation "org.androidannotations:androidannotations-api:4.6.0"
    annotationProcessor "org.androidannotations:androidannotations:4.6.0"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.robertlevonyan.view:MaterialChipView:2.2.7'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'io.github.vicmikhailau:MaskedEditText:4.0.7'
    implementation 'io.github.ShawnLin013:number-picker:2.4.13'
    implementation 'com.github.SamSprung:android-issue-reporter:c83f4b9'
    // Required for Lollipop Amiibo API download compatibility
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
}

configurations {
    all {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
}
