apply plugin: 'com.android.application'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.hiddenramblings.tagmo"
        minSdkVersion 16
        targetSdkVersion 33
        versionName "3.2.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()]
            }
        }

        vectorDrawables {
            useSupportLibrary = true
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "TagMo-" + commit
    }

    flavorDimensions "source"
    productFlavors {
        github {
            getIsDefault().set(true)
        }

        google { }
    }

    def versionCode = defaultConfig.versionName.replace(".","").toInteger()

    buildTypes {
        release {
            defaultConfig.versionCode = versionCode * 10
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".eightbit"
        }
        debug {
            initWith release
            debuggable true
        }
        wearos {
            initWith release
            defaultConfig.versionCode = (versionCode * 10) + 1
            versionNameSuffix "-wear"
        }
    }

    sourceSets {
        release {
            java.srcDirs('src/shared/java')
            res.srcDirs('src/shared/res')
        }
        debug {
            java.srcDirs('src/shared/java')
            res.srcDirs('src/shared/res')
        }
    }

    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
        buildTypes.wearos.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    ndkVersion '21.4.7075529'
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs[0].outputFileName = project.archivesBaseName + ".apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.fragment:fragment:1.5.3'
    implementation 'androidx.webkit:webkit:1.5.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.browser:browser:1.4.0"
    implementation 'com.android.billingclient:billing:5.0.0'
    implementation 'com.google.android.play:core:1.10.3'
    implementation "org.androidannotations:androidannotations-api:4.6.0"
    annotationProcessor "org.androidannotations:androidannotations:4.6.0"
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'
    // Current (3.0.5) requires kotlin (Java is not supported)
    implementation 'com.robertlevonyan.view:MaterialChipView:3.0.4'
    implementation 'io.github.vicmikhailau:MaskedEditText:4.0.7'
    implementation 'io.github.ShawnLin013:number-picker:2.4.13'
    implementation 'com.github.SamSprung:android-issue-reporter:22a3436'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // Required for Lollipop Amiibo API download compatibility
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'

    wearosImplementation "androidx.wear:wear:1.2.0"
}

configurations {
    all {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
}
